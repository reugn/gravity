plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '0.4.1'
    id "me.champeau.gradle.jmh" version "0.5.0"
}

group 'com.github.reugn'
version '0.5.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("user")
    key = properties.getProperty("password")
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'gravity'
        licenses = ['Apache-2.0']
    }
}

ext {
    tikaVersion = '1.24.1'
    slf4jVersion = '1.7.30'
}

dependencies {
    implementation "org.apache.tika:tika-core:${tikaVersion}"
    implementation "org.apache.tika:tika-parsers:${tikaVersion}"
    implementation "com.google.guava:guava:29.0-jre"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    testImplementation "junit:junit:4.13"
    testImplementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
}

sourceSets {
    jmh {
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

test {
    useJUnit()
    maxHeapSize = '128M'
    filter {
        includeTestsMatching "com.github.reugn.gravity.*"
    }
}

jmh {
    include = ['com\\.github\\.reugn\\.gravity\\.bench\\.MatcherBenchmark.*']
    iterations = 1
    fork = 0
    threads = 1
    warmupIterations = 1
}
